plugins {
    id 'groovy'
    id 'maven-publish'
    id 'codenarc'
    id 'com.jfrog.bintray' version '1.3.1'
    id 'nebula.provided-base' version '3.0.3'
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

targetCompatibility = 1.7
group = 'com.craigburke'
version = '1.1.0-SNAPSHOT'

dependencies {
    provided 'com.bertramlabs.plugins:asset-pipeline-core:2.0.12'
    provided 'org.codehaus.groovy:groovy-all:2.4.3'

    compile 'org.asciidoctor:asciidoctorj:1.5.4'

    testCompile 'org.jsoup:jsoup:1.9.1'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}

apply from: 'gradle/coverage.gradle'

codenarc {
    toolVersion = '0.25.2'
    configFile = file('config/codenarc/rules.groovy')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : ''
    key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : ''
    publications = ['maven']

    pkg {
        repo = 'asset-pipeline'
        userOrg = 'craigburke'
        name = 'asciidoctor-asset-pipeline'
        licenses = ['Apache-2.0']
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'asciidoctor-asset-pipeline'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'asciidoctor-asset-pipeline'
                    description 'Asciidoctor extension for the Asset Pipeline Library.'
                    url 'https://github.com/craigburke/asciidoctor-asset-pipeline'
                    scm {
                        url 'https://github.com/craigburke/asciidoctor-asset-pipeline'
                        connection 'scm:https://github.com/craigburke/asciidoctor-asset-pipeline.git'
                        developerConnection 'scm:https://github.com/craigburke/asciidoctor-asset-pipeline.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'craigburke'
                            name 'Craig Burke'
                            email 'craig@craigburke.com'
                        }
                    }
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintrayUpload.dependsOn build, sourcesJar, javadocJar
